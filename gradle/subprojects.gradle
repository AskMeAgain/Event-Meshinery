apply plugin: "checkstyle"
apply plugin: "jacoco"
apply plugin: "java"
apply plugin: "java-library"

task allDeps(type: DependencyReportTask) {}

test {
    useJUnitPlatform()
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${lombok_version}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombok_version}"
    testCompileOnly "org.projectlombok:lombok:${lombok_version}"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
    testImplementation "org.assertj:assertj-core:${assertj_version}"
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation "org.mockito:mockito-inline:${mockito_version}"
    testImplementation "org.springframework.boot:spring-boot-starter-test:${spring_boot_version}"
    testImplementation "org.testcontainers:mysql:${testcontainers_version}"
    testImplementation "org.testcontainers:postgresql:${testcontainers_version}"
    testImplementation "org.testcontainers:kafka:${testcontainers_version}"
    testImplementation "ch.qos.logback:logback-classic:${logback_version}"

    implementation "org.slf4j:slf4j-api:${slf4j_api_version}"
    implementation("org.apache.logging.log4j:log4j-api") {
        version {
            strictly "${log4j_version}"
        }
        because "fixing log4shell exploit"
    }
    implementation("org.apache.logging.log4j:log4j-to-slf4j") {
        version {
            strictly "${log4j_version}"
        }
        because "fixing log4shell exploit"
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

checkstyle {
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
    toolVersion '9.1'
    configFile = rootProject.file('checkstyle.xml')
}

checkstyleTest {
    enabled = false
}

jacocoTestReport {
    enabled true
    dependsOn test
    reports {
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}